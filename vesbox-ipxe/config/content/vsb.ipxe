#!ipxe

:start
echo
echo Volterra iPXE (http://{{cfg.http.server.fqdn}}/vsb.ipxe)
set boot-url http://{{cfg.http.server.fqdn}}/boot
set menu-url http://{{cfg.http.server.fqdn}}/vsb.ipxe
# Figure out if client is 64-bit capable
cpuid --ext 29 && set arch x64 || set arch x86
cpuid --ext 29 && set archl amd64 || set archl i386

:menu-volterra-edge
set menu-timeout 60000
set menu-default ves-ce
menu Volterra Edge (${arch})
item --gap --             [Volterra]
item ves-ce               CE (bootstrap)
#item ves-ce-mini          CE (bootstrap, minimal)
#item ves-ce-live          CE (live)
item ves-re               RE (bootstrap)
#item ves-re-mini          RE (bootstrap, minimal)
#item ves-re-live          RE (live)
item
item --gap --             [Other]
item coreos-stable        CoreOS, stable
item
item --gap --             [Advanced options]
item reload               Reload menu
item reboot               Reboot computer
item --key c config       Configure settings
item shell                Drop to iPXE shell
item
item
item --key 0x08 back             Back to top menu...
item --key x exit                Exit iPXE and continue BIOS boot
choose --timeout ${menu-timeout} --default ${menu-default} selected && goto ${selected} || goto start

## BACKLOG/NOTES
# TODO, efi boot - https://gist.github.com/robinsmidsrod/4008017
# TODO, boot nic - https://gist.github.com/robinsmidsrod/3871687
# packet.com options
  # vga=791 nomodeset splash debug
  # console=ttyAMA0,115200
  # console=ttyS1,115200n8
# debian live-boot options
  # https://manpages.debian.org/testing/live-boot-doc/live-boot.7.en.html
  # https://willhaley.com/blog/custom-debian-live-environment/

## RESCUE
:coreos-stable
echo Booting CoreOS Stable
set base-url http://stable.release.core-os.net/amd64-usr/current
kernel ${base-url}/coreos_production_pxe.vmlinuz coreos.autologin console=ttyS1,115200n8 cloud-config-url=http://matchbox/cloud coreos.autologin console=tty0
initrd ${base-url}/coreos_production_pxe_image.cpio.gz
boot || goto failed
goto start


## SHARED
:debian-live
echo Booting Volterra bootstrap
kernel ${boot-url}/vmlinuz
initrd ${boot-url}/initrd
imgargs vmlinuz boot=live config hooks=filesystem username=live noeject fetch=${boot-url}/live/filesystem.squashfs console=ttyS1,115200n8 console=tty0 ip=frommedia nomodeset
boot || goto failed
goto start

## CE
:ves-ce
set boot-url ${boot-url}/ves-ce
goto debian-live

:ves-ce-mini
set boot-url ${boot-url}/ves-ce-mini
goto debian-live

## RE
:ves-re
set boot-url ${boot-url}/ves-re
goto debian-live

:ves-re-mini
set boot-url ${boot-url}/ves-re-mini
goto debian-live

:config
config
goto start

:reboot
reboot

:exit
exit

:failed
echo Booting failed, dropping to shell
goto shell

:reload
chain --replace --autofree ${menu-url} ||
